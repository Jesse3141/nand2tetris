// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // deal with x input: first zero (a mux), then neg and mux
    Mux16(a=x, b=false, sel=zx, out=xZeroed); 
    Not16(in=xZeroed, out=xNot);
    Mux16(a=xZeroed, b=xNot, sel=nx, out=xProcessed);

    // deal with y input: same as above
    Mux16(a=y, b=false, sel=zy, out=yZeroed); 
    Not16(in=yZeroed, out=yNot);  
    Mux16(a=yZeroed, b=yNot, sel=ny, out=yProcessed); 
 
    // select function: compute both, then mux
    And16(a=xProcessed, b=yProcessed, out=andXY);  
    Add16(a=xProcessed, b=yProcessed, out=addXY);    
    Mux16(a=andXY, b=addXY, sel=f, out=fRes); 
   
    // split fRes for zr and ng
    Not16(in=fRes, out=fResNot);
    //explicicty set output pins to smaller width! 2 8 bit and 1 1.
    Mux16(a=fRes, b=fResNot, sel=no, out[0..7]=outLow, out[8..14]=outHigh, out[15]=outMSB);
    // create correct output format - explicitly set input pins
    Mux16(a[0..7]=outLow, a[8..14]=outHigh, a[15]=outMSB, b=true, sel=false, out=out);

    //zr: check any bit > 0. use or8 to reduce to 1 bit
    Or8Way(in=outLow, out=fResOr1);
    Or8Way(in[0..6]=outHigh, in[7]=outMSB, out=fResOr2);
    Or(a=fResOr1, b=fResOr2, out=xOpYOr);
    Not(in=xOpYOr, out=zr);
    
    // if (out < 0) set ng = 1
    // if the MSB is 1 then it is negative
    Or(a=outMSB, b=false, out=ng);

}   